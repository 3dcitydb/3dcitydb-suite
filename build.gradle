import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'application'
    id 'distribution'
}

configurations {
    izpack
}

dependencies {
    izpack fileTree(dir: 'resources/izpack/lib', include: '*.jar')
}

apply from: 'properties.gradle'

version '4.3.0'

// 3dcitydb project
task set3DCityDBGitBranch(type: Exec) {
    commandLine 'git', 'submodule', 'set-branch', '--branch', project.citydbBranch, '--', '3dcitydb'
}

task update3DCityDBGitSubmodule(type: Exec, dependsOn: set3DCityDBGitBranch) {
    commandLine 'git', 'submodule', 'update', '--init', '--remote', '3dcitydb'
}

task build3DCityDB(type: GradleBuild, group: build, dependsOn: update3DCityDBGitSubmodule) {
    def cityDBProject = project(':citydb')
    tasks = ['installDist']
    buildFile = "$cityDBProject.projectDir/build.gradle"
    outputs.dir "$cityDBProject.projectDir/build/install/3dcitydb"
}

// 3dcitydb-docs project
task set3DCityDBDocsGitBranch(type: Exec) {
    commandLine 'git', 'submodule', 'set-branch', '--branch', project.docsBranch, '--', '3dcitydb-docs'
}

task update3DCityDBDocsGitSubmodule(type: Exec, dependsOn: set3DCityDBDocsGitBranch) {
    commandLine 'git', 'submodule', 'update', '--init', '--remote', '3dcitydb-docs'
}

task build3DCityDBDocs(type: GradleBuild, group: build, dependsOn: update3DCityDBDocsGitSubmodule) {
    def cityDBDocsProject = project(':docs')
    tasks = ['build']
    buildFile = "$cityDBDocsProject.projectDir/build.gradle"
    outputs.dir "$cityDBDocsProject.projectDir/build"
}

// importer-exporter project
task setImporterExporterGitBranch(type: Exec) {
    commandLine 'git', 'submodule', 'set-branch', '--branch', project.impexpBranch, '--', 'importer-exporter'
}

task updateImporterExporterGitSubmodule(type: Exec, dependsOn: setImporterExporterGitBranch) {
    commandLine 'git', 'submodule', 'update', '--init', '--remote', 'importer-exporter'
}

task buildImporterExporter(type: GradleBuild, group: build, dependsOn: updateImporterExporterGitSubmodule) {
    def impExpProject = project(':impexp')
    tasks = ['installDist']
    buildFile = "$impExpProject.projectDir/impexp-client/build.gradle"
    outputs.dir "$impExpProject.projectDir/impexp-client/build/install/3DCityDB-Importer-Exporter"
}

// web-feature-service project
task setWebFeatureServiceGitBranch(type: Exec) {
    commandLine 'git', 'submodule', 'set-branch', '--branch', project.wfsBranch, '--', 'web-feature-service'
}

task updateWebFeatureServiceGitSubmodule(type: Exec, dependsOn: setWebFeatureServiceGitBranch) {
    commandLine 'git', 'submodule', 'update', '--init', '--remote', 'web-feature-service'
}

task buildWebFeatureService(type: GradleBuild, group: build, dependsOn: updateWebFeatureServiceGitSubmodule) {
    def wfsProject = project(':wfs')
    tasks = ['installDist']
    buildFile = "$wfsProject.projectDir/build.gradle"
    outputs.dir "$wfsProject.projectDir/build/install/3DCityDB-Web-Feature-Service"
}

// iur-ade-citydb project
task setIurADEExtensionGitBranch(type: Exec) {
    commandLine 'git', 'submodule', 'set-branch', '--branch', project.iurADEBranch, '--', 'ade-extensions/iur-ade-citydb'
}

task updateIurADEExtensionGitSubmodule(type: Exec, dependsOn: setIurADEExtensionGitBranch) {
    commandLine 'git', 'submodule', 'update', '--init', '--remote', 'ade-extensions/iur-ade-citydb'
}

task buildIurADEExtension(type: GradleBuild, group: build, dependsOn: updateIurADEExtensionGitSubmodule) {
    def iURADEProject = project(':iur-ade')
    tasks = ['installDist']
    buildFile = "$iURADEProject.projectDir/build.gradle"
    outputs.dir "$iURADEProject.projectDir/build/install"
}

// energy-ade-citydb project
task setEnergyADEExtensionGitBranch(type: Exec) {
    commandLine 'git', 'submodule', 'set-branch', '--branch', project.energyADEBranch, '--', 'ade-extensions/energy-ade-citydb'
}

task updateEnergyADEExtensionGitSubmodule(type: Exec, dependsOn: setEnergyADEExtensionGitBranch) {
    commandLine 'git', 'submodule', 'update', '--init', '--remote', 'ade-extensions/energy-ade-citydb'
}

task buildEnergyADEExtension(type: GradleBuild, group: build, dependsOn: updateEnergyADEExtensionGitSubmodule) {
    def energyADEProject = project(':energy-ade')
    tasks = ['installDist']
    buildFile = "$energyADEProject.projectDir/build.gradle"
    outputs.dir "$energyADEProject.projectDir/build/install"
}

// plugin-ade-manager project
task setADEManagerPluginGitBranch(type: Exec) {
    commandLine 'git', 'submodule', 'set-branch', '--branch', project.adePluginBranch, '--', 'importer-exporter-plugins/plugin-ade-manager'
}

task updateADEManagerPluginGitSubmodule(type: Exec, dependsOn: setADEManagerPluginGitBranch) {
    commandLine 'git', 'submodule', 'update', '--init', '--remote', 'importer-exporter-plugins/plugin-ade-manager'
}

task buildADEManagerPlugin(type: GradleBuild, group: build, dependsOn: updateADEManagerPluginGitSubmodule) {
    def adePluginProject = project(':ade-plugin')
    tasks = ['installDist']
    buildFile = "$adePluginProject.projectDir/build.gradle"
    outputs.dir "$adePluginProject.projectDir/build/install"
}

// plugin-spreadsheet-generator project
task setSpreadsheetGeneratorPluginGitBranch(type: Exec) {
    commandLine 'git', 'submodule', 'set-branch', '--branch', project.spshgPluginBranch, '--', 'importer-exporter-plugins/plugin-spreadsheet-generator'
}

task updateSpreadsheetGeneratorPluginGitSubmodule(type: Exec, dependsOn: setSpreadsheetGeneratorPluginGitBranch) {
    commandLine 'git', 'submodule', 'update', '--init', '--remote', 'importer-exporter-plugins/plugin-spreadsheet-generator'
}

task buildSpreadsheetGeneratorPlugin(type: GradleBuild, group: build, dependsOn: updateSpreadsheetGeneratorPluginGitSubmodule) {
    def spshgPluginProject = project(':spshg-plugin')
    tasks = ['installDist']
    buildFile = "$spshgPluginProject.projectDir/build.gradle"
    outputs.dir "$spshgPluginProject.projectDir/build/install"
}

jar.onlyIf {false}
startScripts.onlyIf {false}

distributions.main {
    contents {
        into('3dcitydb') {
            from build3DCityDB.outputs
        }
        into('3dcitydb-docs') {
            from build3DCityDBDocs.outputs
        }
        into('3dcitydb-web-map') {
            from "$rootDir/3dcitydb-web-map"
        }
        into('importer-exporter') {
            from buildImporterExporter.outputs
        }
        into('importer-exporter-plugins') {
            from buildADEManagerPlugin.outputs
            from buildSpreadsheetGeneratorPlugin.outputs
        }
        into('ade-extensions') {
            from buildIurADEExtension.outputs
            from buildEnergyADEExtension.outputs
        }
        into('web-feature-service') {
            from buildWebFeatureService.outputs
        }
    }
}

task createImpExpInstallerContents(dependsOn: buildImporterExporter, type: Copy) {
    into ("$buildDir/tmp/impexp")
    from buildImporterExporter.outputs

    into('3dcitydb') {
        from build3DCityDB.outputs
    }

    into('3d-web-map-client') {
        from "$rootDir/3dcitydb-web-map"
    }

    from(file("$buildDir/tmp/dir")) {
        mkdir "$buildDir/tmp/dir/ade-extensions"
    }

    into ("plugins") {
        from tasks.getByPath('buildADEManagerPlugin').outputs
        from tasks.getByPath('buildSpreadsheetGeneratorPlugin').outputs
    }

    into('manual/3DCityDB_Documentation') {
        from build3DCityDBDocs.outputs
    }
}

task buildImpExpInstaller(dependsOn: createImpExpInstallerContents, group: 'distribution') {
    def impExpProject = project(':impexp')
    impExpProject.apply from: "$impExpProject.projectDir/properties.gradle"

    def installDir = "$buildDir/tmp/impexp".toString()
    def distDir = distZip.destinationDirectory.get()
    def izpackDir = "$buildDir/tmp/izpack".toString()
    def installer = "$distDir/$impExpProject.appName-" + project.version + '-Setup.jar'

    println impExpProject.version

    doLast {
        mkdir distDir

        copy {
            from 'resources/izpack'
            into izpackDir
            filesMatching('**/*.xml') {
                filteringCharset = 'UTF-8'
                filter(ReplaceTokens, tokens: [
                        appName: impExpProject.impexpName,
                        appVersion: impExpProject.version,
                        appSubPath: '3DCityDB-Importer-Exporter',
                        appShortcut: impExpProject.appShortcutName,
                        startScriptName: impExpProject.appName,
                        cliName: impExpProject.appCliName,
                        url: impExpProject.citydbWebsiteUrl,
                        citydbName: impExpProject.citydbName,
                        citydbVersion: impExpProject.citydbVersion,
                        installDir: installDir,
                        cliDir: application.executableDir,
                        pluginsDir: 'plugins',
                        contribsDir: 'contribs',
                        citydbDir: '3dcitydb',
                        adeExtensionsDir: 'ade-extensions',
                        templatesDir: 'templates',
                        manualDir: 'manual',
                        webMapClientDir: '3d-web-map-client',
                        samplesDir: 'samples',
                        spreadSheetPluginDir: 'plugin-spreadsheet-generator',
                        adeManagerPluginDir: 'plugin-ade-manager'
                ])
            }
            filesMatching('**/license.txt') {
                filteringCharset = 'UTF-8'
                filter(ReplaceTokens, tokens: [
                        license: file("$installDir/license/LICENSE.txt").text,
                        apache: file("$rootDir/resources/license/APACHE-2.0.txt").text
                ])
            }
        }

        ant.taskdef(name: 'izpack', classname: 'com.izforge.izpack.ant.IzPackTask', classpath: configurations.izpack.asPath)
        ant.izpack(input: "$izpackDir/install.xml",
                output: installer,
                basedir: izpackDir
        )
    }
}
