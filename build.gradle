import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'application'
    id 'distribution'
}

configurations {
    izpack
}

dependencies {
    izpack fileTree(dir: 'resources/izpack/lib', include: '*.jar')
}

version '4.3.0'

def cityDBProject = project(':3dcitydb')
def cityDBDocsProject = project(':citydb-docs')
def impExpProject = project(':importer-exporter')
def wfsProject = project(':web-feature-service')
def iURADEExtensionProject = project(':iur-ade-citydb')
def energyADEExtensionProject = project(':energy-ade-citydb')
def adeManagerPlugin = project(':ade-manager-plugin')
def spreadsheetGeneratorPluginProject = project(':spreadsheet-generator-plugin')

task build3DCityDB(type: GradleBuild, group: build) {
    tasks = ['clean', 'installDist']
    buildFile = "$cityDBProject.projectDir/build.gradle"
    outputs.dir "$cityDBProject.projectDir/build/install/3dcitydb"
}

task build3DCityDBDocs(type: GradleBuild, group: build) {
    tasks = ['clean', 'build']
    buildFile = "$cityDBDocsProject.projectDir/build.gradle"
    outputs.dir "$cityDBDocsProject.projectDir/build"
}

task buildImporterExporter(type: GradleBuild, group: build) {
    tasks = ['clean', 'installDist']
    buildFile = "$impExpProject.projectDir/impexp-client/build.gradle"
    outputs.dir "$impExpProject.projectDir/impexp-client/build/install/3DCityDB-Importer-Exporter"
}

task buildWebFeatureService(type: GradleBuild, group: build) {
    tasks = ['clean', 'installDist']
    buildFile = "$wfsProject.projectDir/build.gradle"
    outputs.dir "$wfsProject.projectDir/build/install/3DCityDB-Web-Feature-Service"
}

task buildIurADEExtension(type: GradleBuild, group: build) {
    tasks = ['clean', 'installDist']
    buildFile = "$iURADEExtensionProject.projectDir/build.gradle"
    outputs.dir "$iURADEExtensionProject.projectDir/build/install"
}

task buildEnergyADEExtension(type: GradleBuild, group: build) {
    tasks = ['clean', 'installDist']
    buildFile = "$energyADEExtensionProject.projectDir/build.gradle"
    outputs.dir "$energyADEExtensionProject.projectDir/build/install"
}

task buildADEManagerPlugin(type: GradleBuild, group: build) {
    tasks = ['clean', 'installDist']
    buildFile = "$adeManagerPlugin.projectDir/build.gradle"
    outputs.dir "$adeManagerPlugin.projectDir/build/install"
}

task buildSpreadsheetGeneratorPlugin(type: GradleBuild, group: build) {
    tasks = ['clean', 'installDist']
    buildFile = "$spreadsheetGeneratorPluginProject.projectDir/build.gradle"
    outputs.dir "$spreadsheetGeneratorPluginProject.projectDir/build/install"
}

jar.onlyIf {false}
startScripts.onlyIf {false}

distributions.main {
    contents {
        into('3dcitydb') {
            from build3DCityDB.outputs
        }
        into('3dcitydb-docs') {
            from build3DCityDBDocs.outputs
        }
        into('3dcitydb-web-map') {
            from "$rootDir/3dcitydb-web-map"
        }
        into('importer-exporter') {
            from buildImporterExporter.outputs
        }
        into('importer-exporter-plugins') {
            from buildADEManagerPlugin.outputs
            from buildSpreadsheetGeneratorPlugin.outputs
        }
        into('ade-extensions') {
            from buildIurADEExtension.outputs
            from buildEnergyADEExtension.outputs
        }
        into('web-feature-service') {
            from buildWebFeatureService.outputs
        }
    }
}

task createImpExpInstallerContents(dependsOn: buildImporterExporter, type: Copy) {
    into ("$buildDir/tmp/impexp")
    from buildImporterExporter.outputs

    into('3dcitydb') {
        from build3DCityDB.outputs
    }

    into('3d-web-map-client') {
        from "$rootDir/3dcitydb-web-map"
    }

    from(file("$buildDir/tmp/dir")) {
        mkdir "$buildDir/tmp/dir/ade-extensions"
    }

    into ("plugins") {
        from tasks.getByPath('buildADEManagerPlugin').outputs
        from tasks.getByPath('buildSpreadsheetGeneratorPlugin').outputs
    }

    into('manual/3DCityDB_Documentation') {
        from build3DCityDBDocs.outputs
    }
}

task buildImpExpInstaller(dependsOn: createImpExpInstallerContents, group: 'distribution') {
    impExpProject.apply from: "$impExpProject.projectDir/properties.gradle"
    def installDir = "$buildDir/tmp/impexp".toString()
    def distDir = distZip.destinationDirectory.get()
    def izpackDir = "$buildDir/tmp/izpack".toString()
    def installer = "$distDir/$impExpProject.appName-" + impExpProject.version + '-Setup.jar'

    doLast {
        mkdir distDir

        copy {
            from 'resources/izpack'
            into izpackDir
            filesMatching('**/*.xml') {
                filteringCharset = 'UTF-8'
                filter(ReplaceTokens, tokens: [
                        appName: impExpProject.impexpName,
                        appVersion: impExpProject.version,
                        appSubPath: '3DCityDB-Importer-Exporter',
                        appShortcut: impExpProject.appShortcutName,
                        startScriptName: impExpProject.appName,
                        cliName: impExpProject.appCliName,
                        url: impExpProject.citydbWebsiteUrl,
                        citydbName: impExpProject.citydbName,
                        citydbVersion: impExpProject.citydbVersion,
                        installDir: installDir,
                        cliDir: application.executableDir,
                        pluginsDir: 'plugins',
                        contribsDir: 'contribs',
                        citydbDir: '3dcitydb',
                        adeExtensionsDir: 'ade-extensions',
                        templatesDir: 'templates',
                        manualDir: 'manual',
                        webMapClientDir: '3d-web-map-client',
                        samplesDir: 'samples',
                        spreadSheetPluginDir: 'plugin-spreadsheet-generator',
                        adeManagerPluginDir: 'plugin-ade-manager'
                ])
            }
            filesMatching('**/license.txt') {
                filteringCharset = 'UTF-8'
                filter(ReplaceTokens, tokens: [
                        license: file("$installDir/license/LICENSE.txt").text,
                        apache: file("$rootDir/resources/license/APACHE-2.0.txt").text
                ])
            }
        }

        ant.taskdef(name: 'izpack', classname: 'com.izforge.izpack.ant.IzPackTask', classpath: configurations.izpack.asPath)
        ant.izpack(input: "$izpackDir/install.xml",
                output: installer,
                basedir: izpackDir
        )
    }
}
